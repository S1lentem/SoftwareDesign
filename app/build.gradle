apply plugin: 'com.android.application'

def getVersion(){
    def cmd = 'git log --pretty=format:"%h%d" --all'
    Process process = cmd.execute()
    String[] commits = process.in.text.split('\\n')
    def patch = 0
    String majorMinor
    for (commit in commits){
        if (commit =~ /tag: \b\d+[.]\d+\b/) {
            majorMinor = commit.find(/\b\d+[.]\d+\b/)
            break
        }
        patch++
    }
    return majorMinor + "." + patch
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.artem.softwaredesign"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName getVersion()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        defaultConfig {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.defaultConfig
        }
    }

    android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "FirstLab-${variant.versionName}.apk"
            }
    }
    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            versionNameSuffix "-dev"
        }
        master {
            dimension "version"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compile 'com.android.support:design:28.0.0'
}
